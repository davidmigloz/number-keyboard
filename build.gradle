plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.gradle) apply false
    alias(libs.plugins.kotlin.compose) apply false
}

ext {
    javaVersion = JavaVersion.VERSION_21
    jvmToolchainVersion = 21

    androidConfig = [
            minSdkVersion    : 21,
            targetSdkVersion : 36,
            compileSdkVersion: 36
    ]

    /**
     * Build number (currently not available in JitPack).
     */
    getBuildNumber = { ->
        return System.getenv()["buildNumber"] as Integer ?: 0
    }

    /**
     * Version code: MMMmmppbbb
     */
    generateVersionCode = { versionMajor, versionMinor, versionPatch ->
        Integer versionCode = versionMajor * 10000000 +
                versionMinor * 100000 +
                versionPatch * 1000 +
                (getBuildNumber() % 1000)
        println "Version code: " + versionCode
        return versionCode
    }

    /**
     * Version name: <major>.<minor>.<patch>[-<verClass>]
     */
    generateVersionName = { versionMajor, versionMinor, versionPatch, versionClassifier ->
        String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
        if (versionClassifier != null) {
            versionName += "-" + versionClassifier
        }
        println "Version name: " + versionName
        return versionName
    }
}


allprojects {
    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}
